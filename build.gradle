plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'com.gluonhq.gluonfx-gradle-plugin' version '1.0.22'
}

group 'com.nbeghin.ccv2.api.gui'
version '1.0.7'
description 'A simple cross-platform JavaFX GUI for managing SAP (Hybris) Commerce Cloud'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

ext {
    junitVersion = '5.10.0'
    url = 'https://github.com/nicolabeghin/sap-commerce-cloud-api-gui/'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'sap.commerce.cloud.management.api.gui.main'
    mainClass = 'com.nbeghin.ccv2.api.gui.sapcommercecloudapigui.App'
}

run {
    // https://github.com/controlsfx/controlsfx/wiki/Using-ControlsFX-with-JDK-9-and-above
    jvmArgs = ['--add-exports=javafx.base/com.sun.javafx.event=org.controlsfx.controls']
}
gluonfx {
    mainClassName = application.mainClass.get()
    enableSwRendering = true
    releaseConfiguration.skipSigning = true
    releaseConfiguration.vendor = 'nicolabeghin'
    releaseConfiguration.packageType = 'dmg'
    releaseConfiguration.version = project.version
    releaseConfiguration.versionCode = project.version
    releaseConfiguration.description = project.description
    releaseConfiguration.versionName = project.version
    reflectionList = [
            'com.nbeghin.ccv2.api.gui.sapcommercecloudapigui.JavaFXApplication',
            'javafx.fxml.FXMLLoader',
            'javafx.geometry.Insets',
            'javafx.scene.control.MenuBar',
            'javafx.scene.control.Menu',
            'javafx.scene.control.MenuItem',
            'javafx.scene.control.TabPane',
            'javafx.scene.control.Tab',
            'javafx.scene.control.TableView',
            'javafx.scene.control.Label',
            'javafx.scene.control.Button',
            'javafx.scene.control.Tooltip',
            'javafx.scene.control.TextField',
            'javafx.scene.control.CheckBox',
            'javafx.scene.control.ProgressBar',
            'javafx.scene.control.TextArea',
            'javafx.scene.control.ComboBox',
            'javafx.scene.layout.VBox',
            'javafx.scene.layout.AnchorPane',
            'javafx.scene.layout.HBox',
            'javafx.scene.layout.Pane',
            'com.nbeghin.ccv2.api.gui.sapcommercecloudapigui.MainController',
            'com.nbeghin.ccv2.api.gui.sapcommercecloudapigui.BuildController',
            'com.nbeghin.ccv2.api.gui.sapcommercecloudapigui.SettingsController'
    ]
}
javafx {
    version = '22.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

// sync gradle properties to a properties file read by app at runtime
task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/app.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p['description'] = project.description.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

dependencies {
    implementation('org.controlsfx:controlsfx:11.2.1')
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation "com.squareup.retrofit2:converter-scalars:2.11.0"
    implementation "com.squareup.retrofit2:converter-gson:2.11.0"
    implementation "io.swagger.core.v3:swagger-annotations:2.0.0"
    implementation ("org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:1.0.2") {
        exclude group: "org.json", module: "json"
        exclude group: "org.apache.oltu.oauth2", module: "org.apache.oltu.oauth2.common"
    }
    implementation 'org.json:json:20231013'
    implementation "javax.annotation:jsr250-api:1.0"
    implementation "io.gsonfire:gson-fire:1.8.0"
    implementation "org.threeten:threetenbp:1.3.5"

    implementation 'ch.qos.logback:logback-core:1.5.6'
    implementation 'ch.qos.logback:logback-classic:1.5.6'
    implementation 'org.slf4j:slf4j-api:2.0.13'
    // https://github.com/spring-projects/spring-boot/issues/37152#issuecomment-1719671229
//    implementation 'org.apache.logging.log4j:log4j-core:2.19.0'
//    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.19.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
}
